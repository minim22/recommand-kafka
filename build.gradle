plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
}

group = 'com.custom'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

bootJar {
    enabled = false
}

jar {
    enabled = true

    // 1. 파일명 앞부분 (recommend-kafka)
    archiveBaseName = 'recommend-kafka'
    
    // 2. 버전 (0.0.1-SNAPSHOT)
    archiveVersion = '0.0.1-SNAPSHOT'
    
    // 3. 꼬리표 제거 (중요! 이걸 비워야 '-plain'이 안 붙습니다)
    archiveClassifier = ''
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            // version을 명시적으로 선언 안된애들은 runtime classpath 해결된 버전을 사용
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionOf('runtimeClasspath')
                }
            }
        }
    }
}

dependencies {
    api 'org.springframework.kafka:spring-kafka'
    
    api 'com.fasterxml.jackson.core:jackson-databind'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    
    implementation 'org.slf4j:slf4j-api'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}